/*
 * Copyright (c) 2015 by Advay Mengle.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

plugins {
    id "com.madvay.tools.build.gitbuildinfo" version "0.1.3-alpha"
}

buildStamp {
    // The git commit SHA will be appended to this url to generate the final url.
    repoBaseUrl "https://github.com/madvay/android-analysis-tools/tree/"
    // BuildInfo.java will be added under this package.
    packageName "com.madvay.tools.android.perf"
}

group 'com.madvay.tools.android'
version '0.2.1'

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'application'

mainClassName = 'com.madvay.tools.android.perf.apat.Main'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.google.guava:guava:18.0'
    compile('com.android.tools.ddms:ddmlib:24.3.1') {
        exclude group: 'net.sf.kxml'
        exclude group: 'com.android.tools'
    }
}


task deleteProguardDestDir << {
    project.file('build/distributions-mini/lib').deleteDir()
}
task proguardPreDist(type: proguard.gradle.ProGuardTask) {
    dependsOn 'deleteProguardDestDir'
    injars jar.archivePath
    injars project.configurations.runtime
    configuration file('proguard.pro')
    dependsOn 'jar'
    outjars 'build/distributions-mini/lib/'
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
}

tasks.remove(distTar)
tasks.remove(distZip)
tasks.remove(installDist)
tasks.remove(assembleDist)
tasks.remove(installApp)

distributions {
    remove(getByName('main'))
    mini {
        baseName = "${project.name}"
        contents {
            into('lib') {
                from(proguardPreDist)
            }
            from(project.file("src/dist"))
            into("bin") {
                from(startScripts)
                fileMode = 0755
            }
        }
    }
}

installMiniDist.dependsOn(proguardPreDist)
miniDistTar.dependsOn(proguardPreDist)
miniDistZip.dependsOn(proguardPreDist)

jar {
    manifest {
        attributes(
                ['Specification-Title'  : 'apat - Android Performance Analysis Tools',
                 'Specification-Version': version,
                 'Specification-Vendor' : 'https://github.com/madvay/android-analysis-tools'
                ],
                'com/madvay/tools/android/perf/apat/'
        )
    }
}

run {
    if (project.hasProperty('apatArgs')) {
        args project.apatArgs.split(' ')
    }
}

